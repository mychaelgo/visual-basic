'Deskripsi: Menghitung selisih dua buah tanggal yang diketahui 
'           lalu menampilkan hasilnya dalam bentuk selisih hari
'           dan selisih durasi jam lengkapnya. Contoh: Jika 
'           tanggal awal  = 01/03/2002 17:18:00 dan 
'           tanggal akhir = 01/09/2002 09:42:30, maka akan
'           menghasilkan --> 183 hari, 16:24:30 
'           Artinya: (183 hari, 16 jam, 24 menit, dan 30 detik).
'           Tips ini menggunakan fungsi DateDiff
'Pembuat  : Masino Sinaga (admin@masinosinaga.com)
'Diupload : Minggu, 1 September 2002
'Persiapan: 1. Buat 1 Project baru dengan 1 Form.
'           2. Tambahkan 2 TextBox, 1 Label, dan 1 Timer.
'           3. Copy-kan coding berikut ke dalam editor form yang bertalian.
'--------------------------------------------------------------------------

Option Explicit

Function SelisihHariJam(ByVal Awal As Date, _
                        ByVal Akhir As Date) As String

Dim Detik As Long, Hari As Long, Jam As Long
Dim JamLengkap As String
   
  If Awal > Akhir Then
     MsgBox "Tanggal dan waktu awal harus lebih kecil " & vbCrLf & _
            "dari pada tanggal dan waktu akhir", _
            vbCritical, "Peringatan"
     Exit Function
  End If
  
  'Tampung dalam durasi satuan terkecil, yaitu: DETIK
  Detik = DateDiff("s", Awal, Akhir)
  
  'Hitung jumlah jam dgn cara membagi 3600
  '(backslash ("\") supaya menghasilkan
  'nilai Integer tanpa pembulatan ke atas)
  Jam = Detik \ 3600
  
  'Jika jumlah jam lebih besar dari 23
  'artinya: lebih dari 1 hari
  If Jam > 23 Then
     
     'Hitung jumlah hari dgn car membagi 24
     '(backslash ("\") supaya menghasilkan
     'nilai integer tanpa pembulatan ke atas)
     Hari = Jam \ 24
     
     'Hitung Durasi Jam dalam hh:mm:ss
     JamLengkap = Format((Akhir - Awal), "hh:mm:ss")
  
  Else 'Jika jumlah jam <= 23
      
     Hari = 0   'maka jumlah hari = nol
      
     'Hitung Durasi Jam dalam hh:mm:ss
     JamLengkap = Format((Akhir - Awal), "hh:mm:ss")
  End If
  
  If Hari = 0 Then  'Jika jumlah hari = 0
     
     'Tampung hasil akhirnya
     SelisihHariJam = JamLengkap
  
  Else  'Jika jumlah hari > 0, tampilkan jumlah harinya
     
     'Tampung hasil akhirnya
     SelisihHariJam = Hari & " hari, " & JamLengkap
  End If
  Exit Function

End Function

Private Sub Form_Load()
  Timer1.Interval = 500
  Timer1.Enabled = True
  Text1.Text = "01/03/2002 07:18:00"
  'Text2.Text = "01/09/2002 09:42:30"
  Text2.Text = Now
End Sub

Private Sub Timer1_Timer()
On Error GoTo Pesan
  Text2.Text = Now
  Label1.Caption = SelisihHariJam(CDate(Text1.Text), _
                      CDate(Text2.Text))
  Exit Sub
Pesan:
  MsgBox "Tanggal atau format-nya salah!", _
         vbCritical, "Error Tanggal"
End Sub